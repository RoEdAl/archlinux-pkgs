pkgname=libubox
pkgver=20170915
pkgrel=1
pkgdesc='C utility functions for OpenWrt/LEDE'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://git.lede-project.org/?p=project/libubox.git;a=summary'
license=('GPLv2')
makedepends=('cmake>=2.8.0' 'git')
source=('libubox::git://git.lede-project.org/project/libubox.git');
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  git log -1 --format=%cd --date=short|tr -d -
}

prepare() {
  cd "${srcdir}"
  [ -d "${pkgname}-build" ] || mkdir "${pkgname}-build"
  cd "${pkgname}-build"

  cmake ../${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=MINSIZEREL \
    -DBUILD_LUA=OFF \
    -DBUILD_EXAMPLES=OFF
}

build() {
  cd "${srcdir}/${pkgname}-build"
  make
}

package() {
  cd "${srcdir}/${pkgname}-build"
  make DESTDIR="${pkgdir}" install
}



_commit=ac9940370cce01e00bb3628c5b714402af725b13
pkgname=upmpdcli
pkgver=1.2.12
pkgrel=1
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.lesbonscomptes.com/upmpdcli/"
license=('GPL2')
makedepends=('git' 'automake')
depends=('libmpdclient' 'libupnpp>=0.15.1' 'libmicrohttpd' 'jsoncpp')
optdepends=('mpd' 'python2')
backup=('etc/upmpdcli.conf')
install=upmpdcli.install
source=("git://github.com/medoc92/upmpdcli.git#commit=${_commit}"
        mpd.png
        upmpdcli.service
	)

sha256sums=('SKIP'
            'c256258aefa9aa0347504e197aef9426849403dc0c64638fc154613a62d4c21c'
            '53e2f0a33e3692d9e33102ab44130060b7311c5dbea4cb0091528223706c04e7')

prepare() {
        cd "${srcdir}/${pkgname}"
        ./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}"
	./configure --sysconfdir=/etc --prefix=/usr
	make src/config.h
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="$pkgdir" install
	install -Dm644 ../mpd.png ${pkgdir}/usr/share/upmpdcli/icon.png
	install -Dm644 ../upmpdcli.service ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
}

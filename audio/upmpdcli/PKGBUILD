# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname=upmpdcli
pkgver=1.4.7
pkgrel=1
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=('x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.lesbonscomptes.com/upmpdcli/"
license=('LGPL')
makedepends=('git' 'automake')
depends=('libmpdclient' 'libupnpp>0.18' 'libmicrohttpd' 'jsoncpp' 'curl')
optdepends=('mpd' 'python2')
backup=('etc/upmpdcli.conf')
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/${pkgname}-${pkgver}.tar.gz"
        'mpd.png'
        'upmpdcli.service'
	'upmpdcli.sysusers'
	'0001-Remove-DEBUG-macro.patch'
)

sha256sums=('55dec356c98e16f00b1e547f9f4a1c03756317102e91982f93eca8cc4eda115e'
            'c256258aefa9aa0347504e197aef9426849403dc0c64638fc154613a62d4c21c'
            '89b56845c3e937471769bc9507519f1ec27d0b64ebbb455e0ea06a7ad46c6f70'
            'ade3c6c8edf0b13c9c8bacd1d3cec85b0eef82563fee9a3d12244d83f56cfb59'
            'f4d5fc241a45f458b37d96f3ca45163aaa33f70fcba8673fb9fb5ac0290c6482')

prepare() {
        cd "${srcdir}/${pkgname}-${pkgver}"

	git apply ../0001-Remove-DEBUG-macro.patch
        ./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --sysconfdir=/etc --prefix=/usr --disable-spotify
	make src/config.h
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	# additional files
	install -Dm644 ../mpd.png ${pkgdir}/usr/share/upmpdcli/icon.png
	install -Dm644 ../upmpdcli.service ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
	install -Dm644 ../upmpdcli.sysusers ${pkgdir}/usr/lib/sysusers.d/upmpdcli.conf

	# tweak persmissions of configuration file
	chmod 0644 ${pkgdir}/etc/upmpdcli.conf

	# remove all plugins
	rm -rf ${pkgdir}/usr/share/upmpdcli/cdplugins/*
	rmdir ${pkgdir}/usr/share/upmpdcli/cdplugins
}

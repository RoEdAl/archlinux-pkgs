
_commit=ea9316ecae9417ed015bcffe0296d5abcf0a0d8c
pkgname=upmpdcli
pkgver=1.2.15
pkgrel=4
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="http://www.lesbonscomptes.com/upmpdcli/"
license=('GPL2')
makedepends=('git' 'automake')
depends=('libmpdclient' 'libupnpp>=0.16.0' 'libmicrohttpd' 'jsoncpp')
optdepends=('mpd' 'python2')
backup=('etc/upmpdcli.conf')
install=upmpdcli.install
source=("upmpdcli::git+http://@opensourceprojects.eu/git/p/upmpdcli/code#commit=${_commit}"
        mpd.png
        upmpdcli.service
	ndebug.patch )

sha256sums=('SKIP'
            'c256258aefa9aa0347504e197aef9426849403dc0c64638fc154613a62d4c21c'
            '53e2f0a33e3692d9e33102ab44130060b7311c5dbea4cb0091528223706c04e7'
            '2b40461f3f68f5816a167e4a3658e7cc816536e9c5728e2844d710b9f5e1d192')

prepare() {
        cd "${srcdir}/${pkgname}"
	patch -Np1 -i ../ndebug.patch
        ./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}"
	./configure --sysconfdir=/etc --prefix=/usr
	make src/config.h
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="$pkgdir" install
	install -Dm644 ../mpd.png ${pkgdir}/usr/share/upmpdcli/icon.png
	install -Dm644 ../upmpdcli.service ${pkgdir}/usr/lib/systemd/system/upmpdcli.service

	# remove all plugins
        rm -rf ${pkgdir}/usr/share/upmpdcli/cdplugins/*
}

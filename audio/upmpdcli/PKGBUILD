# Maintainer: Mansour Behabadi <mansour@oxplot.com>

_commit=b771bbf4bbe4c05c5e163488460820326fcdd456
_srcname=upmpdcli-${_commit}
pkgname=upmpdcli
pkgver=1.1.3
pkgrel=1
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.lesbonscomptes.com/upmpdcli/"
license=('GPL2')
makedepends=('automake')
depends=('libmpdclient' 'libupnpp>=0.13.1')
optdepends=('mpd' 'python2')
backup=('etc/upmpdcli.conf')
install=upmpdcli.install
source=(
	"http://github.com/medoc92/upmpdcli/archive/${_commit}.tar.gz"
        mpd.png
        upmpdcli.service
	)

sha256sums=('9d745bec9997b67b5aeaea4e571e08a087c72686e1bdfee275fbbdaaea79a348'
            'c256258aefa9aa0347504e197aef9426849403dc0c64638fc154613a62d4c21c'
            '53e2f0a33e3692d9e33102ab44130060b7311c5dbea4cb0091528223706c04e7')

build() {
	cd "${srcdir}/${_srcname}"
	./autogen.sh
	./configure --sysconfdir=/etc --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${_srcname}"
	make DESTDIR="$pkgdir" install
	install -Dm644 ../mpd.png ${pkgdir}/usr/share/upmpdcli/icon.png
	install -Dm644 ../upmpdcli.service ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
}

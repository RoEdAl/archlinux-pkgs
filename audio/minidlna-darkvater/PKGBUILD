# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:Biginoz < biginoz AT free point fr>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>
# Contributor: Matthias Sobczyk <matthias.sobczyk@googlemail.com>
# Contributor: Kamil Kaminski <kyle@kkaminsk.com>

pkgname=minidlna-darkvater
_pkgver=1.1.6
pkgver=1.1.6.r869
pkgrel=5
pkgdesc="A DLNA/UPnP-AV Media server (aka ReadyDLNA) - Darkvater branch"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://sourceforge.net/projects/minidlna/"
license=('GPL')
depends=('libexif' 'libjpeg' 'libid3tag' 'flac' 'libvorbis' 'ffmpeg' 'sqlite')
makedepends=('git')
optdepends=('libiconv')
conflicts=('minidlna')
replaces=('minidlna')
provides=("minidlna=${_pkgver}")
backup=('etc/minidlna.conf')
install=minidlna-darkvater.install
source=('minidlna-darkvater::git+http://github.com/Darkvater/minidlna.git#branch=features'
	minidlna.service
	minidlna.tmpfiles
	minidlna.sysusers
	free-childrens-once-only.patch
	albumart-serve.patch
	find-embedded-art.patch
	optimize-art-cache.patch)
md5sums=('SKIP'
         '1f53adef7dfa12c1d4636737366b1d39'
         'a61f97b037ea17555f6632b3ffd761ef'
         '5b2a57160589dfd559a23b1a7f365a23'
         '1e70541c3399b553cf95b05b6bf11b4c'
         '96a2ece256c0cf688d6ecae917ac70c0'
         '839bca9e16e3500a8bc05e8c4f679330'
         '7ceef01a63e96b5f8b5d5c9de56a0c7e')

pkgver() {
  cd "$pkgname"
  printf "${_pkgver}.r%s" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "$srcdir/$pkgname"
  sed -i 's|#user=.*|user=minidlna|g' minidlna.conf
  patch -Np1 -i ../free-childrens-once-only.patch
  patch -Np1 -i ../albumart-serve.patch
  patch -Np1 -i ../find-embedded-art.patch
  patch -Np1 -i ../optimize-art-cache.patch
}

build() {
  cd "$srcdir/$pkgname"
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname"
  DESTDIR="$pkgdir" make install
  install -Dm644 minidlna.conf "$pkgdir"/etc/minidlna.conf
  install -Dm0644 "$srcdir"/minidlna.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/minidlna.conf
  install -Dm0644 "$srcdir"/minidlna.sysusers "$pkgdir"/usr/lib/sysusers.d/minidlna.conf
  install -Dm0644 "$srcdir"/minidlna.service "$pkgdir"/usr/lib/systemd/system/minidlna.service
  install -Dm644 "$srcdir"/$pkgname/minidlna.conf.5 "$pkgdir"/usr/share/man/man5/minidlna.conf.5
  install -Dm644 "$srcdir"/$pkgname/minidlnad.8 "$pkgdir"/usr/share/man/man8/minidlnad.8
}

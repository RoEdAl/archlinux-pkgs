# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

pkgname=libupnpp
pkgver=0.17.2
pkgrel=1
pkgdesc="A C++ wrapper for libupnp - used with upmpdcli or upplay"
url="https://www.lesbonscomptes.com/upmpdcli/index.html"
arch=('x86_64' 'i686' 'armv6h' 'armv7h' 'aarch64')
license=('GPL2')
makedepends=('git' 'automake')
depends=('libupnp' 'expat' 'curl')
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/${pkgname}-${pkgver}.tar.gz"
	'0001-Remove-DEBUG-macro.patch')
sha256sums=('5abaaf353a1e9c3482d61ef2627b650285d59f27c1ee60d35b8951952261374f'
            '11de7b0e23ab63289ab9e07ef1ef059833344a1794e7f25e969815c6e13a014a')

prepare(){
	cd "${srcdir}/${pkgname}-${pkgver}"

	git apply ../0001-Remove-DEBUG-macro.patch
	./autogen.sh
}

build(){
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr --sysconfdir=/etc
	make libupnpp/config.h
	make
}

package(){
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="$pkgdir" install
}

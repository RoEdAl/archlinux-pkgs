_commit=423769ecd822436533e91db0712082760f7acd39
pkgname=libupnpp
pkgver=0.15.1
pkgrel=4
pkgdesc="A C++ wrapper for libupnp - used with upmpdcli or upplay"
url="https://github.com/medoc92/libupnpp"
arch=('x86_64' 'i686' 'armv6h' 'armv7h')
license=('GPL2')
makedepends=('git' 'automake')
depends=('libupnp' 'expat' 'curl')
source=("git://github.com/medoc92/libupnpp.git#commit=${_commit}"
	'ndebug.patch'
	'missing-macros.patch')
sha256sums=('SKIP'
            'fff9b9305e3ee33f3e61e55800be577d1f69408a9a5c07f0f6962069f99060fc'
            '04fe8e0d7034c7fb903345174ef1d54f9a0d681830ebfcdfa5017513bf85af80')

prepare(){
	cd "${srcdir}/${pkgname}"
	patch -Np1 -i ../ndebug.patch
	patch -Np1 -i ../missing-macros.patch
	./autogen.sh
}

build(){
	cd "${srcdir}/${pkgname}"
	./configure --prefix=/usr --sysconfdir=/etc
	make libupnpp/config.h
	make
}

package(){
	cd "${srcdir}/${pkgname}"
	make DESTDIR="$pkgdir" install
}

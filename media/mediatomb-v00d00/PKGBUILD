pkgname=mediatomb-v00d00
_pkgver=0.13.0
pkgver=0.13.0.r2328
pkgrel=2
pkgdesc='Free UPnP/DLNA media server'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://github.com/v00d00/mediatomb'
license=('GPL')
depends=('libupnp-git>=1.8.0' 'libutil-linux' 'expat' 'sqlite3' 'curl' 'taglib>=1.11.1' 'file' 'ffmpeg' 'libexif')
makedepends=('cmake' 'git')
backup=('etc/conf.d/mediatomb')
conflicts=('mediatomb')
replaces=('mediatomb')
provides=("mediatomb=${_pkgver}")
install=mediatomb.install
source=("mediatomb-v00d00::git+http://github.com/v00d00/mediatomb"
        'mediatomb.sysusers'
        'mediatomb.service'
        'mediatomb.conf'
        'upnp-message.path')
sha256sums=('SKIP'
            '99e2602eebb9f5236107b0bee473a4187c7df7732745ad83542a3dcca9cb7d5a'
            'ce31379ebdbad1dead652ac209ee479e83a2e0ac09fc88690b83b675d82324e9'
            'adc46d69ab240bb907fd363b13035d88e1a1cda2d6e223692c9a01d8e1309707'
            '10aadea967e068f6220936bd96189aa657476d3871aca4ecfeeb89b3c83f6338')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "${_pkgver}.r%s" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -Np1 -i ../upnp-message.path

  cd ..
  mkdir build
  cd build

  cmake ../${pkgname} -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release\
        -DWITH_MAGIC=1 \
        -DWITH_MYSQL=0 \
        -DWITH_CURL=1 \
        -DWITH_JS=0 \
        -DWITH_TAGLIB=1 \
        -DWITH_AVCODEC=1 \
        -DWITH_DVDNAV=0 \
        -DWITH_EXIF=1 \
        -DWITH_LASTFM=0 \
	-DWITH_LOGGING=0 \
	-DWITH_DEBUG_LOGGING=1
}

build() {
  cd "${srcdir}/build"
  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/mediatomb.sysusers" "${pkgdir}/usr/lib/sysusers.d/mediatomb.conf"
  install -Dm644 "${srcdir}/mediatomb.service" "${pkgdir}/usr/lib/systemd/system/mediatomb.service"
  install -Dm644 "${srcdir}/mediatomb.conf" "${pkgdir}/etc/conf.d/mediatomb"
}

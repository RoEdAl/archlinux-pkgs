# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:Biginoz < biginoz AT free point fr>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>
# Contributor: Matthias Sobczyk <matthias.sobczyk@googlemail.com>
# Contributor: Kamil Kaminski <kyle@kkaminsk.com>

pkgname=minidlna-darkvater
_pkgver=1.1.6
pkgver=1.1.6.r876
pkgrel=1
pkgdesc="A DLNA/UPnP-AV Media server (aka ReadyDLNA) - Darkvater branch"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://sourceforge.net/projects/minidlna/"
license=('GPL')
depends=('libexif' 'libjpeg' 'libid3tag' 'flac' 'libvorbis' 'ffmpeg' 'sqlite' 'wavpack')
makedepends=('git')
optdepends=('libiconv')
conflicts=('minidlna')
replaces=('minidlna')
provides=("minidlna=${_pkgver}")
backup=('etc/minidlna.conf')
install=minidlna-darkvater.install
source=('minidlna-darkvater::git+http://github.com/RoEdAl/minidlna.git#branch=features'
	minidlna.service
	minidlna-r.service
	minidlna.tmpfiles
	minidlna.sysusers)
md5sums=('SKIP'
         '552fdccd165aa82290eeb05b5186a6e5'
         '059aad6f266234b7a55f5999f89d787d'
         'a61f97b037ea17555f6632b3ffd761ef'
         '5b2a57160589dfd559a23b1a7f365a23')

pkgver() {
  cd "$pkgname"
  printf "${_pkgver}.r%s" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "$srcdir/$pkgname"
  sed -i 's|#user=.*|user=minidlna|g' minidlna.conf
}

build() {
  cd "$srcdir/$pkgname"
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname"
  DESTDIR="$pkgdir" make install
  install -Dm644 minidlna.conf "$pkgdir"/etc/minidlna.conf
  install -Dm0644 "$srcdir"/minidlna.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/minidlna.conf
  install -Dm0644 "$srcdir"/minidlna.sysusers "$pkgdir"/usr/lib/sysusers.d/minidlna.conf
  install -Dm0644 "$srcdir"/minidlna.service "$pkgdir"/usr/lib/systemd/system/minidlna.service
  install -m0644 "$srcdir"/minidlna-r.service "$pkgdir"/usr/lib/systemd/system/minidlna-r.service
  install -Dm644 "$srcdir"/$pkgname/minidlna.conf.5 "$pkgdir"/usr/share/man/man5/minidlna.conf.5
  install -Dm644 "$srcdir"/$pkgname/minidlnad.8 "$pkgdir"/usr/share/man/man8/minidlnad.8
}
